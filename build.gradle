/*
 * Copyright (c) 2025 Command-Analyzer Contributors.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 *
 */

plugins {
    id 'java-library'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.2' //Please Comment Out
    id 'com.vanniktech.maven.publish' version '0.34.0' //Please Comment Out
}

version = '2.0.0-rc2'
group = 'io.github.udo-nabe'

//Please Comment Out: Begin
mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    coordinates(
            project.group,
            project.rootProject.name,
            project.version
    )

    pom {
        name = project.rootProject.name
        description = 'A lightweight library for parsing basic command options.'
        inceptionYear = '2025'
        url = 'https://github.com/udo-nabe/Command-Analyzer'
        licenses {
            license {
                name = 'MIT License'
                url = 'https://github.com/udo-nabe/Command-Analyzer/blob/main/LICENSE'
                distribution = 'repo'
            }
        }

        developers {
            developer {
                id = 'udo-nabe'
                name = 'Udo-Nabe'
                url = 'https://github.com/udo-nabe'
            }
        }

        scm {
            connection = 'scm:git:https://github.com/udo-nabe/Command-Analyzer.git'
            developerConnection = 'scm:git:git@github.com:udo-nabe/Command-Analyzer.git'
            url = 'https://github.com/udo-nabe/Command-Analyzer'
        }
    }
}
//Please Comment Out: End

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.13.4')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
}

idea.project.settings {
    copyright {
        useDefault = 'MIT License'
        profiles {
            'MIT License' {
                notice = '''\
Copyright (c) 2025 Command-Analyzer Contributors.
                
This source code is licensed under the MIT license found in the
LICENSE file in the root directory of this source tree.
'''.stripIndent()
                keyword = 'Copyright'
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
